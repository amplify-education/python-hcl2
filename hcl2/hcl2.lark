start : body
body : (attribute | block)? (/\n/ (attribute | block))*
attribute : identifier "=" expression
block : identifier (identifier | STRING_LIT)* "{" body "}"
new_line_and_or_comma: /(,|\n)/

identifier : /[a-zA-Z_][a-zA-Z0-9_-]*/

?expression : conditional | expr_term | operation

conditional : expression "?" expression ":" expression

?operation : unary_op | binary_op
!unary_op : ("-" | "!") expr_term
binary_op : expression binary_term
!binary_operator : "==" | "!=" | "<" | ">" | "<=" | ">=" | "-" | "*" | "/" | "%" | "&&" | "||" | "+"
binary_term : binary_operator expression

expr_term : "(" expression ")"
            | float_lit
            | int_lit
            | STRING_LIT
            | tuple
            | object
            | function_call
            | index_expr_term
            | get_attr_expr_term
            | identifier
            | heredoc_template
            | heredoc_template_trim
            | attr_splat_expr_term
            | full_splat_expr_term
            | for_tuple_expr
            | for_object_expr

STRING_LIT  : "\"" (STRING_LIT_CHAR | INTERPOLATION)* "\""
STRING_LIT_CHAR : /[^"\\]|\\./ // Any character except " or \, or an escaped character
NESTED_INTERPOLATION : "${" /[^}]+/ "}"
INTERPOLATION : "${" (/(?:(?!\${)([^}]))+/ | NESTED_INTERPOLATION)+ "}"

int_lit : DECIMAL+
!float_lit: DECIMAL+ "." DECIMAL+ (EXP_MARK DECIMAL+)?
            | DECIMAL+ ("." DECIMAL+)? EXP_MARK DECIMAL+
DECIMAL : "0".."9"
EXP_MARK : ("e" | "E") ("+" | "-")?

tuple : "[" (expression ",")* expression? "]"
object : "{" (object_elem (new_line_and_or_comma object_elem )* new_line_and_or_comma?)? "}"
object_elem : (identifier | expression) ("=" | ":") expression

heredoc_template : /<<(?P<heredoc>[a-zA-Z][a-zA-Z0-9._-]+)\n(?:.|\n)*?(?P=heredoc)/
heredoc_template_trim : /<<-(?P<heredoc_trim>[a-zA-Z][a-zA-Z0-9._-]+)\n(?:.|\n)*?(?P=heredoc_trim)/

function_call : identifier "(" arguments? ")"
arguments : (expression ("," expression)* ("," | "...")?)

index_expr_term : expr_term index
get_attr_expr_term : expr_term get_attr
attr_splat_expr_term : expr_term attr_splat
full_splat_expr_term : expr_term full_splat
index : "[" expression "]" | "." DECIMAL+
get_attr : "." identifier
attr_splat : ".*" get_attr*
full_splat : "[*]" (get_attr | index)*

!for_tuple_expr : "[" for_intro expression for_cond? "]"
!for_object_expr : "{" for_intro expression "=>" expression "..."? for_cond? "}"
!for_intro : "for" identifier ("," identifier)? "in" expression ":"
!for_cond : "if" expression

%ignore /[ \t]+/
%ignore /\/\*(.|\n)*?(\*\/)/
%ignore /#.*\n/ | /\/\/.*\n/
%ignore /\n/
